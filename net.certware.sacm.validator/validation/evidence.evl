/**
 * Validation rules and fixes for SACM models.
 * Epsilon Validation Language
 * @author mrb
 */
 
// argumentation nodes
context Evidence {

	// must have an identifier
	constraint HasIdentifier {
		check : self.identifier.isDefined()
		message : 'Argument ' + self.eClass().name + ' requires identifier'
	}
	
	// must have content 
	// fix sets content to TBS and sets the associated flag
	constraint HasContent {
		guard : self.satisfies('HasIdentifier')
		check : self.content.isDefined()
		message : 'Argument ' + self.identifier + ' requires content'
		fix {
			title : 'Add TBS to Argument ' + self.identifier
			do {
				self.content := 'TBS';
				self.toBeSupported := true;
			}
		}
	}
	
	// the argument should have at least one solution, strategy, or sub-argument
	critique HasSolutions {
		guard : self.satisfies('HasIdentifier')
		check : (not self.argumentEvidence.isEmpty()) or (not self.argumentClaims.isEmpty()) 
		message : 'Argument ' + self.identifier + ' has no evidence or claims'
		fix {
			title : 'Set the to-be-supported flag as a reminder'
			do {
				self.toBeSupported := true;
			}
		}
	}
} // argument context


