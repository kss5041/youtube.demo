/*
 * Draft intent specification model.
 * This grammar represents the structure and some semantics but not executable requirements.
 * Based loosely on SpecTRM model from various publications.
 */
grammar net.certware.intent.IntentSpecification with org.eclipse.xtext.common.Terminals

generate intentSpecification "http://www.certware.net/intent/IntentSpecification"

/*
 * An intent specification consists of one or more refinements.
 */
Specification:
	'specification'
	name=ID
	refinements+=Refinement+
;

/*
 * A refinement has no type and consists of one or more intents.
 */
Refinement:
	'refinement'
	name=ID
	desc=STRING
	intents+=Intent+	
;	

/*
 * An intent has a type and consists of one or more decompositions. 
 */
Intent:
	type=IntentType
	name=ID
	desc=STRING
	decompositions+=Decomposition+	
;

/*
 * Intent types refer to the different levels of the canonical model.
 * The grammar does not enforce their universal containment in a specification.
 * A proper intention specification should contain an intent of each type in each refinement.
 */
IntentType:
	typeName=('basic' | 'purpose' | 'principles' | 'models' | 'design' | 'implementation' | 'operation')
;

/*
 * A decomposition has a type and consists of one or more models, documents, or items.
 */
Decomposition:
	type=DecompositionType
	name=ID
	desc=STRING
	('{' documents+=Document* '}')?
	('[' models+=ModelItem* ']')?
	('(' items+=ListItem* ')')?
;

DecompositionType:
	typeName=('environment' | 'operator' | 'system' | 'verification')
;

Document:
	'{' entries += DocItem+ '}'
;

DocItemType:
	typeName=('condition' | 'figure' | 'break' | 'model' | 'paragraph' | 'section' | 'table')
;

DocItem :
	type=DocItemType
	name=ID
	ref=STRING
	';'
;

ModelType:
	typeName=('output' | 'mode' | 'state' | 'macro' | 'function' | 'input')	
;

ModelItem:
	type=ModelType
	name=ID
	desc=STRING
	';'
;

ListItemType:
	typeName=('requirement' | 'goal' | 'hazard' | 'constraint')	
;

/*
 * A list item has a type, description and lists of references.
 */
ListItem:
	type=ListItemType
	name=ID
	desc=STRING
	('{' docReferences+=[DocItem]+ '}' )?
	('(' itemReferences+=[ListItem]+ ')' )?
	('[' modelReferences+=[ModelItem]+ ']' )?
	';'
;

